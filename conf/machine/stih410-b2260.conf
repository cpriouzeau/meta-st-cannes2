#@TYPE: Machine
#@NAME: stih410-b2260
#@DESCRIPTION: Machine configuration for ST Cannes2  systems to build with Linux-STI Kernel 4.6 support

# =========================================================================
# Chip architecture
# =========================================================================
require conf/machine/include/tune-cortexa9.inc
include conf/machine/include/gpu_mali.inc

# =========================================================================
# Machine settings
# =========================================================================
# Default machine feature
MACHINE_FEATURES = "usbhost usbgadget alsa screen bluetooth wifi ext2"

# Default serial consoles (TTYs) to enable using getty
SERIAL_CONSOLES = "115200;ttyAS1"

# Ship all kernel by default
MACHINE_EXTRA_RRECOMMENDS = " \
    kernel-modules            \
    ${@bb.utils.contains('DISTRO_FEATURES', 'bluetooth', 'rtl8723bu-stih410-b2260', '', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'bluetooth', 'linux-firmware-rtl8723bu', '', d)} \
    ${@bb.utils.contains('MACHINE_FEATURES', 'gpu', 'mali400-driver-stih410-b2260 ', '', d)} \
    ${@bb.utils.contains('MACHINE_FEATURES', 'gpu', '${GPU_USERLAND_LIBRARIES_INSTALL}', '', d)} \
    linux-sti-firmware-fdma \
    "

# =========================================================================
# Xserver
# =========================================================================
XSERVER ?= " \
    xserver-xorg \
    xserver-xorg-module-libint10 \
    xf86-input-evdev \
    xf86-video-modesetting \
"

# =========================================================================
# Image
# =========================================================================
# BSP default settings
IMAGE_CLASSES += "image_types-st"
IMAGE_FSTYPES_append = " ext4.gz stimg "

# =========================================================================
# Kernel
# =========================================================================
PREFERRED_PROVIDER_virtual/kernel = "linux-sti"
PREFERRED_VERSION_linux-sti       = "4.19"

SERIAL_CONSOLE = "115200 ttyAS1"

# Kernel image type
KERNEL_IMAGETYPE     =  "uImage"
KERNEL_ALT_IMAGETYPE = " vmlinux "

# Kernel loadaddr
ST_KERNEL_LOADADDR  = "0x40008000"

# List of device tree to install
KERNEL_DEVICETREE = " stih410-b2260.dtb "

# =========================================================================
# Kernel COMMAND LINE
# key work:
#   ##ROOT##: key work will should be replaced by device name like /dev/sda1
#   ##MEMORY##: key work will should be replaced by content of KERNEL_CMDLINE_MEMORY
# =========================================================================
KERNEL_CMDLINE_MEMORY = "mem=1024M@0x40000000"
KERNEL_CMDLINE = "console=ttyAS1,115200 CONSOLE=/dev/ttyAS1 consoleblank=0 root=##ROOT## rootfstype=ext4 rw rootwait ${KERNEL_CMDLINE_MEMORY} vmalloc=256m  "
KERNEL_CMDLINE_DEV_APPEND = "clk_ignore_unused"

# =========================================================================
# U boot
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-sti"
# Configuration
UBOOT_CONFIG = "b2260"
EXTRA_IMAGEDEPENDS += "u-boot-sti"

# =========================================================================
# GPU
# =========================================================================
# Issue with libmali No gpu alvailable
GPU_USERLAND_LIBRARIES_WAYLAND = "mesa"
# if GPU (mali400) are activated and EULA is accepted
#MACHINE_FEATURES =+ "${@'gpu' if d.getVar('ACCEPT_EULA_'+d.getVar('MACHINE', True), True) == '1' else ''}"
